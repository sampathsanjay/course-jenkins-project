pipeline {
  agent any

  environment {
    SERVER_IP  = credentials('Prod-server-ip')
    APP_DIR    = '01-jenkins-basics'
    ARCHIVE    = 'myapp.tar.gz'
    REMOTE_DIR = 'app'
    SERVICE    = 'flaskapp.service'
  }

  stages {

    stage('Setup') {
      steps {
        dir("${env.APP_DIR}") {
          sh '''
            python3 -m venv .venv
            . .venv/bin/activate
            pip install --upgrade pip
            pip install -r requirements.txt
          '''
        }
      }
    }

    stage('Test') {
      steps {
        dir("${env.APP_DIR}") {
          sh '''
            . .venv/bin/activate
            pytest -q
          '''
        }
      }
    }

    stage('Package') {
      steps {
        dir("${env.APP_DIR}") {
          sh """
            rm -f ${env.ARCHIVE}
            tar --warning=no-file-changed \
                --exclude=.git \
                --exclude=.venv \
                --exclude=venv \
                --exclude=__pycache__ \
                --exclude='*.pyc' \
                -czf ${env.ARCHIVE} .
          """
        }
      }
    }

    stage('Deploy to Prod') {
      input {
        message "Deploy to ${env.SERVER_IP}?"
        ok "Yes, Deploy"
      }

      steps {
        withCredentials([
          sshUserPrivateKey(
            credentialsId: 'SSH-KEY',
            keyFileVariable: 'MY_SSH_KEY',
            usernameVariable: 'SSH_USER'
          )
        ]) {

          sh """
            scp -i \$MY_SSH_KEY -o StrictHostKeyChecking=no \
              ${env.APP_DIR}/${env.ARCHIVE} \
              \$SSH_USER@${env.SERVER_IP}:/home/\$SSH_USER/
          """

          sh """
            ssh -i \$MY_SSH_KEY -o StrictHostKeyChecking=no \$SSH_USER@${env.SERVER_IP} << EOF
              set -e

              mkdir -p /home/\$SSH_USER/${env.REMOTE_DIR}
              tar -xzf /home/\$SSH_USER/${env.ARCHIVE} -C /home/\$SSH_USER/${env.REMOTE_DIR}

              cd /home/\$SSH_USER/${env.REMOTE_DIR}

              python3 -m venv venv
              . venv/bin/activate
              pip install --upgrade pip
              pip install -r requirements.txt

              sudo systemctl restart ${env.SERVICE}
              sudo systemctl is-active --quiet ${env.SERVICE}

              echo "Deployment Successful!"
EOF
          """
        }
      }
    }
  }

  post {
    always {
      cleanWs()
    }
  }
}
